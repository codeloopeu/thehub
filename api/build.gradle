plugins {
    id 'org.springframework.boot' version '2.0.5.RELEASE'
    id 'org.jetbrains.kotlin.jvm' version '1.2.70'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.2.71'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.jmailen.kotlinter' version '1.20.0'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'io.spring.dependency-management'

defaultTasks 'boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileKotlin {
    kotlinOptions{
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

sourceSets {
    integrationTest {
        kotlin {
            srcDirs += 'src/it/kotlin'
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources.srcDirs += 'src/it/resources'
    }
}

task integrationTest(group: 'verification', type: Test, description: 'Runs integration tests.') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task stage(dependsOn: ['assemble'])

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    reports {
        csv.enabled false
        html.enabled true
        xml.enabled true
    }
    dependsOn test
}

task ktLint(type: org.jmailen.gradle.kotlinter.tasks.LintTask, group: 'verification', description: 'Kotlin lint') {
    source = file('src')
    continuationIndentSize = 4
    reports = [:]
}

kotlinter {
    continuationIndentSize = 4
}

check {
    dependsOn = check.dependsOn + [ktLint, integrationTest]
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'http://dl.bintray.com/kotlin/kotlinx.coroutines' }
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    ext.kotlinxcoroutines = '0.30.1'

    compile 'org.springframework.boot:spring-boot-starter-webflux'

    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.70'
    compile 'org.jetbrains.kotlin:kotlin-reflect:1.2.70'
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxcoroutines"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinxcoroutines"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-reactor:$kotlinxcoroutines"

    compile 'org.flywaydb:flyway-core:5.2.0'
    compile 'org.postgresql:postgresql:42.2.5'
    compile 'javax.validation:validation-api:2.0.1.Final'

    compile 'com.fasterxml.jackson.module:jackson-module-kotlin'
    compile 'org.jsoup:jsoup:1.11.3'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'io.projectreactor:reactor-test'
    testCompile 'com.nhaarman:mockito-kotlin:1.6.0'
    testCompile 'org.powermock:powermock-api-mockito2:2.0.0-beta.5'
    testCompile 'org.assertj:assertj-core:3.11.1'

    integrationTestCompile 'com.despegar:spark-test:1.1.8'
    integrationTestCompile 'com.jayway.jsonpath:json-path:2.4.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}
